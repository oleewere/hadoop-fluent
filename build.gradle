/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath "com.netflix.nebula:gradle-ospackage-plugin:4.4.0"
    classpath 'de.undercouch:gradle-download-task:3.2.0'
  }
}

apply plugin: "nebula.ospackage"

subprojects {

  repositories {
    mavenCentral()
    jcenter()
  }

  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'maven'

  group 'com.cloudera.hadoop.client'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  task copyDeps(type: Copy) {
    from configurations.compile
    into "$buildDir/libs"
  }

  copyDeps.dependsOn compileJava

  dependencies {
    compile group: 'org.apache.hadoop', name: 'hadoop-common', version: "${hadoopVersion}"
  }
}

task processHadoopClients(type:Copy) {
  into "build/hadoop-cloud-clients"
  with copySpec {
    from "${projectDir}/hadoop-abfs-client/build/libs"
    into { "libs/hadoop-abfs-client" }
  }
  with copySpec {
    from "${projectDir}/hadoop-gcs-client/build/libs"
    into { "libs/hadoop-gcs-client" }
  }
  with copySpec {
    from "${projectDir}/hadoop-s3-client/build/libs"
    into { "libs/hadoop-s3-client" }
  }
  with copySpec {
    from "${projectDir}/hadoop-wasb-client/build/libs"
    into { "libs/hadoop-wasb-client" }
  }
  with copySpec {
    from "${projectDir}/scripts/hadoop-client.sh"
    into { "bin/" }
  }
}

task distTar(type: Tar, dependsOn: ['processHadoopClients']){
  extension = 'tar.gz'
  compression = Compression.GZIP
  baseName = "hadoop-cloud-clients"
  from "build/hadoop-cloud-clients"
  doLast {
    ant.checksum(file: "${buildDir}/distributions/hadoop-cloud-clients-${version}.tar.gz", algorithm: "SHA-1", fileext: ".sha")
  }
}


