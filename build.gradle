/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath "com.netflix.nebula:gradle-ospackage-plugin:4.4.0"
    classpath 'de.undercouch:gradle-download-task:3.2.0'
  }
}

apply plugin: "nebula.ospackage"

subprojects {

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }

  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'maven'

  group 'com.cloudera.hadoop.cloud'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  task copyDeps(type: Copy) {
    from configurations.compile
    into "$buildDir/libs"
  }

  copyDeps.dependsOn compileJava
}

task processHadoopClients(type:Copy) {
  into "build/hadoop-fluent"
  with copySpec {
    from "${projectDir}/hadoop-fluent-forward/build/libs"
    into { "libs/core" }
  }
  with copySpec {
    from "${projectDir}/hadoop-cloud-abfs/build/libs"
    into { "libs/abfs" }
  }
  with copySpec {
    from "${projectDir}/hadoop-cloud-gcs/build/libs"
    into { "libs/gcs" }
  }
  with copySpec {
    from "${projectDir}/hadoop-cloud-s3/build/libs"
    into { "libs/s3" }
  }
  with copySpec {
    from "${projectDir}/hadoop-cloud-wasb/build/libs"
    into { "libs/wasb" }
  }
  with copySpec {
    from "${projectDir}/scripts/hadoop-fluent.sh"
    into { "bin/" }
  }
}

task distTar(type: Tar, dependsOn: ['processHadoopClients']){
  extension = 'tar.gz'
  compression = Compression.GZIP
  baseName = "hadoop-fluent"
  from "build/hadoop-fluent"
  doLast {
    ant.checksum(file: "${buildDir}/distributions/hadoop-fluent-${version}.tar.gz", algorithm: "SHA-1", fileext: ".sha")
  }
}

ext.distPackageName = "hadoop-fluent"
ext.hadoopFluentDescription = "Hadoop Fluent"
ext.buildVersion = "${version}"
ext.buildNumber = "${buildNumber}"
ext.hadoopFluentFolder = "/usr/lib/hadoop-fluent"

ospackage {
  packageName = "${distPackageName}"
  version = "${buildVersion}"
  release = "${buildNumber}"
  into "${hadoopFluentFolder}"
  os = LINUX
  user = 'root'
  permissionGroup = 'root'
  license = "APL2"
  url = "https://hadoop.apache.org/"
  summary = ""
  from("build/distributions/hadoop-fluent-${buildVersion}.tar.gz") {
    into "${distPackageName}"
  }
}

task rpm(type: Rpm, dependsOn: [distTar]) {
  packageDescription = "${hadoopFluentDescription}"
}

task deb(type: Deb, dependsOn: [distTar]) {
  packageDescription = "${hadoopFluentDescription}".replaceAll("\n", "\n:")
}


